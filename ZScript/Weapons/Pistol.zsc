//Vengeance Stryker Sidearm

Class PistolIsEmpty : MO_ZSToken{}

Class NeverUsedPistol : MO_ZSToken{}

class MO_VengeanceStryker : MO_Weapon// replaces Pistol
{
	int burstcounter;
	const MaxBurst = 3;
	bool burstSelected;
 	Default
	{
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive2 10;
		Weapon.SelectionOrder 1900;
		Weapon.AmmoType1 "PistolMagAmmo";
		Weapon.AmmoType2 "MO_LowCaliber";
		Obituary "$OBMO_VENPISTOL";
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.NO_AUTO_SWITCH;
		+WEAPON.NOAUTOFIRE;
		+WEAPON.AMMO_OPTIONAL
		Inventory.Pickupmessage "$GOTVENPISTOL";
		Tag "$TAG_VENPISTOL";
		Scale .55;
		Inventory.PickupSound "weapons/pistol/pickup";
		Inventory.ForbiddenTo "MO_SergeantPlayer";
		Inventory.RestrictedTo "MO_OfficerPlayer";
		+WEAPON.NOALERT;
		MO_Weapon.InspectToken "NeverUsedPistol";
	}

	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		A_GiveInventory("NeverUsedPistol",1);
		burstcounter = 0;
	}

	action void MO_SetBurstMode(bool b = false)
	{
		invoker.burstSelected = b;
	}

	action bool MO_GetBurstMode()
	{
		return invoker.burstSelected;
	}

	action void MO_ResetBurstCount()
	{
		invoker.burstcounter = 0;
	}

	action bool MO_IsMaxBurstCount()
	{
		return invoker.burstcounter >= MaxBurst;
	}

	action void MO_CountBurst()
	{
		invoker.burstcounter++;
	}

	action void MO_FirePistol()
	{
		MO_UseAmmo();
		A_FireBullets (5.6, 0, 1, 14, "UpdatedBulletPuff", FBF_NORANDOM,0,"MO_BulletTracer", 0);
		A_StartSound("weapons/pistol/fire", CHAN_WEAPON);
		A_AlertMonsters();
		A_GunFlash();
		JM_CheckForQuadDamage();
	}

	action void MO_SetSpriteIfEmpty(string lump)
	{
		if(invoker.Ammo1.amount < 1) {MO_SetWeaponSprite(lump, OverlayID());}
	}
		
	States
	{
	Ready:
	ReadyToFire:
		PSTD D 0 A_JumpIfInventory("PistolMagAmmo",1,2);
		PSTF E 0;
		#### # 1 
		{
			if(invoker.isZoomed) {
			return ResolveState("Ready2");
			}
			if(PressingAltFire() && invoker.ADSMode >= 1) {
			return ResolveState("AltFire");
			}
			if(JustPressed(BT_ALTATTACK) && invoker.ADSMode < 1) {
			return ResolveState("AltFire");
			}
			if(PressingFire() && invoker.Ammo1.amount > 0) {
			return ResolveState("Fire");
			}
			A_SetCrossHair(invoker.GetXHair(0));
			return JM_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
		Loop;

	Inspect:
			TNT1 A 8;
			TNT1 A 0 A_StartSound("pistol/inspect",0);
			PS1I ABCDEFGHIIJJJJJJ 1 JM_WeaponReady();
			TNT1 A 0 A_StartSound("weapons/pistol/magout",0);
			PS1I KMNPRSTVW 1 JM_WeaponReady();
			PS1I WWWWW 3 JM_WeaponReady();
			PS1I XXYZ 1 JM_WeaponReady();
			2S1I ABCD 1;
			PSTG A 0 A_StartSound("weapons/pistol/magin",0);
			2S1I DEEFG 1;
			PSTG A 0 A_StartSound("weapons/pistol/magclick",0);
			2S1I HIJKLLL 1 JM_WeaponReady();
			PSTG A 0 A_StartSound("Weapons/pistol/silencerfoley",7);
			2S1I MNOP 1 JM_WeaponReady();
			PSTG A 0 A_StartSound("Weapons/pistol/chamberback",0);
			2S1I QRSTTUU 1 JM_WeaponReady();
			2S1I UUUUUUUUUUU 1;
			2S1I V 1 A_StartSound("Weapons/pistol/chamberforward",0);
			2S1I WXY 1 JM_WeaponReady();
			2S1I Z 5 JM_WeaponReady();
			PS1I F 1  A_StartSound("pistol/reloadend",0);
			PSR1 EDCBA 1 JM_WeaponReady();
			TNT1 A 0 A_SetInventory("SpecialAction",0);
			Goto Ready;

	Select:
		PSTG A 0;
		PSTG A 0 A_SetCrossHair(invoker.GetXHair(0));
	ContinueSelect:
		//Load sprites into memory
		PS1K ABCDEF 0;
		PSR2 A 0;
		PSZE ABC 0;
		TNT1 A 1 MO_Raise;
		TNT1 A 0 JM_CheckInspectIfDone;
	SelectAnimation:
		TNT1 A 0 A_StartSound("weapons/pistol/draw",1);
		TNT1 A 0 MO_JumpIfLessAmmo(where: "SelectEmpty");
		PSTD ABC 1;
		Goto Ready;
	SelectEmpty:
		PSTD EFG 1;
		Goto Ready;

	Deselect:
		PSTG A 0 A_SetCrossHair(invoker.GetXHair(0));
		TNT1 A 0 
		{
			invoker.isZoomed = false;
			invoker.isHoldingAim = False;
			A_ZoomFactor(1);
		}
		TNT1 A 0 MO_JumpIfLessAmmo(where: "DeselectEmpty");
		PSTD CBA 1;
	ChangeWeaponNow:
		PSTG A 0 A_Lower(12);
		Wait;

	DeselectEmpty:
		PSTD GFE 1;
		Goto ChangeWeaponNow;

	Fire:
		PSTG A 0 MO_JumpIfLessAmmo(where: "Empty");
		PSTG A 0
		{
			if(invoker.isZoomed) {return ResolveState("Fire2");}
			if(MO_GetBurstMode()) {return ResolveState("BurstFire");}
			return ResolveState(null);
		}
		PSTF A 1 MO_FirePistol();
		PSTG A 0 MO_EjectCase("PistolCasing",26, 0, 9, 0, frandom(3, 5), frandom(3,4));
		PSTF B 1 JM_GunRecoil(-0.3, .06);
		PSTF C 1 JM_GunRecoil(+0.125, .01);
		PSTG A 0 MO_JumpIfLessAmmo();
		PSTF D 1;
		PSTD D 0;
 		PSTD DDDDDDDD 1 {
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
			}
		PSTG A 0 A_JumpIf(PressingFire(), "Fire");
		Goto Ready;

	Fire2: //ADS Fire
		PSTG A 0 MO_JumpIfLessAmmo(where: "Empty");
		PSTG A 0 A_JumpIf(MO_GetBurstMode(), "BurstFire2");
		PSZG B 1 MO_FirePistol;
		PSTG A 0 MO_EjectCase("PistolCasing",28,2, 3, 0, frandom(3, 5), frandom(3,4));
	Fire2.Animation:
		PSZG C 1 JM_GunRecoil(-0.2, .06);
		PSZG D 1 JM_GunRecoil(+0.05, .01);
		PSTG A 0 MO_JumpIfLessAmmo;
		PSZG E 1;
		PSZG AAAAAAAA 1 {
				if(invoker.ADSMode >= 1)
				{
					if(PressingAltFire() && JustPressed(BT_ATTACK))
					{
						invoker.isHoldingAim = true;
						return ResolveState("Fire2");
					}
					else
					{if(JustPressed(BT_ATTACK)) {return ResolveState("Fire2");}}
				}
				else
				{if(JustPressed(BT_ATTACK)) {return ResolveState("Fire2");}}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
			}
		PISG A 0
		{
			if(MO_GetBurstMode()) {Return ResolveState("BurstFireFinished2Loop");}
			if(invoker.ADSMode >= 1)
				{
					if(PressingAltFire() && PressingFire())
					{
						invoker.isHoldingAim = true;
						return ResolveState("Fire2");
					}
					if(!PressingAltFire() && invoker.isHoldingAim) {
						return ResolveState("UnZoom");
					}
					else
					{if(PressingFire()) {return ResolveState("Fire2");}}
				}
				else
				{if(PressingFire()) {return ResolveState("Fire2");}}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
		}
		Goto Ready;

	Empty:
		PSZG F 0 A_JumpIf(invoker.ammo1.amount < 1 && invoker.isZoomed, 2);
		PSTF E 0;
		#### # 0 A_JumpIf(invoker.ammo2.amount >= 1, "Reload");
		#### # 0 A_StartSound("weapon/pistolempty",0);
		#### # 1;
		#### # 0 A_Jumpif(invoker.isZoomed && invoker.isHoldingAim, "ADSHold");
		Goto ReadyToFire;

	BurstFire2:
		PSTG A 0 MO_JumpIfLessAmmo(where: "Empty");
		PSZG B 1 
		{
			MO_FirePistol();
			MO_CountBurst();
		}
		PSTF A 0 A_JumpIf(MO_IsMaxBurstCount(), "BurstFire2Finished");
		PSZG D 1 
		{
			MO_EjectCase("PistolCasing",28,2, 3, 0, frandom(3, 5), frandom(3,4));
			JM_GunRecoil(-0.275, .06);		
		}
		Loop;
	
	BurstFire2Finished:
		TNT1 A 0 MO_ResetBurstCount;
		Goto Fire2.Animation;
	BurstFireFinished2Loop:
		PSZG A 1;
		PISG A 0
		{
			if(invoker.ADSMode >= 1)
				{
					if(PressingAltFire() && PressingFire())
					{
						invoker.isHoldingAim = true;
						return ResolveState("BurstFireFinished2Loop");
					}
					if(!PressingAltFire() && invoker.isHoldingAim) {
						return ResolveState("UnZoom");
					}
					else
					{if(PressingFire()) {return ResolveState("BurstFireFinished2Loop");}}
				}
				else
				{if(PressingFire()) {return ResolveState("BurstFireFinished2Loop");}}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
		}
		Goto ReadyToFire;

	AltFire:
			TNT1 A 0 A_JumpIf(invoker.isZoomed, "UnZoom");
		Zoom:
			PSZA A 0 MO_SetSpriteIfEmpty("PSZE");
			#### A 0 {invoker.isZoomed = true;}
			#### A 0 A_StartSound("weapon/adsup",0);
			#### A 0 MO_ZoomBob();
			#### A 0 A_ZoomFactor(1.3);
			#### A 0 A_SetCrosshair(99);
			#### ABC 1;
		Ready2:
			PSTG A 0 A_JumpIf(invoker.ammo1.amount >=1, 2);
			PSZG F 0;
			TNT1 # 0 A_JumpIf(invoker.ADSMode <= 0, "ADSToggle");
			TNT1 # 0 A_JumpIf(invoker.ADSMode == 1, "ADSHold");
			PSZG ##### 1 
			{
				if(JustPressed(BT_ATTACK)) {return ResolveState("Fire");}
				if(PressingFire() && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 # 0 A_JumpIf(invoker.ADSMode == 2 && PressingAltFire() , "ADSHold");

		ADSToggle:
			PSZG # 1 
			{
				if(JustPressed(BT_ALTATTACK)) {return ResolveState("UnZoom");}
				if(PressingFire() || JustPressed(BT_ATTACK) && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;
		//Hold and Hybrid
		ADSHold:
			TNT1 # 0 {invoker.isHoldingAim = true;}
			PSZG # 1 
			{
				if(!PressingAltFire()) {return ResolveState("UnZoom");}
				if(PressingFire() && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;

		UnZoom:
			TNT1 A 0 
			{
				invoker.isZoomed = False;
				invoker.isHoldingAim = False;
				MO_ResetBob();
			}
			PSZA A 0 MO_SetSpriteIfEmpty("PSZE");
			#### A 0 A_StartSound("weapon/adsdown",0);
			#### A 0 A_ZoomFactor(1.0);
			#### A 0 A_SetCrossHair(invoker.GetXHair(0));
			#### CBA 1;
			PSTF E 0 A_JumpIf(invoker.Ammo1.amount < 1, 2);
			PSTD D 0;
			#### ##### 1
			{
				if(PressingFire()) {return ResolveState("Fire");}
				if(JustPressed(BT_ALTATTACK)) {Return ResolveState("AltFire");}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY);
			Goto ReadyToFire;

	BurstFire:
		PSTG A 0 MO_JumpIfLessAmmo(where: "Empty");
		PSTF A 1 
		{
			MO_FirePistol();
			MO_CountBurst();
		}
		PSTG A 0 MO_EjectCase("PistolCasing",26, 0, 9, 0, frandom(3, 5), frandom(3,4));
		PSTF A 0 A_JumpIf(MO_IsMaxBurstCount(), "BurstFinished");
		PSTF C 1 JM_GunRecoil(-0.45, .06);		
		Loop;
	
	BurstFinished:
		TNT1 A 0 MO_ResetBurstCount;
		PSTF B 1 JM_GunRecoil(-0.45, .06);
		PSTF C 1 JM_GunRecoil(+0.2, .01);
		PSTG A 0 MO_JumpIfLessAmmo();
		PSTF D 1;
		PSTD DDDDDDDD 1 {
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
			}
		BurstFinishedLoop:
		PSTD D 1;
		PSTD D 0
		{
			if(PressingFire()) {Return ResolveState("BurstFinishedLoop");}
			return ResolveState(null);
		}
		Goto ReadyToFire;


	ActionSpecial:
		PSTK A 0 MO_SetSpriteIfEmpty("PS1K");
		#### A 0
		{
			if(!MO_GetBurstMode()) 
			{	
				A_Print("Burst Fire", 1);
				MO_SetBurstMode(true);
			}
			else
			{
				A_Print("Semi Auto", 1);
				MO_SetBurstMode(false);
			}
		}
		#### ABCDE 1;
		#### F 2;
		#### F 2 A_StartSound("weapons/smg/modeswitch",0);
		#### EDCBA 1;
		#### A 0 A_SetInventory("SpecialAction",0);
		Goto ReadyToFire;

	Reload:
		PSTG A 0 
		{
			MO_ResetBurstCount();
			MO_ResetBurstCount();
		}
		PSTG A 0 MO_CheckReload(17, 1, "EmptyReload", "TacticalReload", "ReadyToFire");
		PSR1 A 0;
	ReloadAnimation:
		#### A 0 A_StartSound("pistol/reloadlift",CHAN_AUTO);
		#### A 0 A_ZoomFactor(1.0);
		#### A 0 A_SetCrossHair(invoker.GetXHair(0));
		#### A 0 {invoker.isZoomed = false;}
		#### A 0 A_JumpIf(invoker.OwnerHasSpeed(), "FastReload");
		#### ABC 1 JM_WeaponReady(WRF_NOFIRE);
		#### DEF 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magout",CHAN_AUTO);
		#### GH 1 JM_WeaponReady(WRF_NOFIRE);
		#### H 9 JM_WeaponReady(WRF_NOFIRE);
		#### I 2 JM_WeaponReady(WRF_NOFIRE);
		#### JK 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magin",CHAN_AUTO);
		#### KLMMN 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magclick",CHAN_7);
		#### OO 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_JumpIfInventory("PistolIsEmpty",1,"ChamberReload");
		#### A 0 JM_ReloadGun("PistolMagAmmo", "MO_LowCaliber",18,1);
		#### PPQR 1;
	DoneReload:
	//	Goto ReloadLoop;
		#### A 0 A_StartSound("pistol/reloadendfast",CHAN_AUTO);
		#### STUV 1;
		TNT1 A 0;
		Goto ReadyToFire;

	FastReload:
		#### ABC 1 JM_WeaponReady(WRF_NOFIRE);
		#### EF 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magout",CHAN_AUTO);
		#### G 1 JM_WeaponReady(WRF_NOFIRE);
		#### H 6 JM_WeaponReady(WRF_NOFIRE);
		#### IJK 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magin",CHAN_AUTO);
		#### LMMN 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_StartSound("weapons/pistol/magclick",CHAN_7);
		#### A 0 A_JumpIfInventory("PistolIsEmpty",1,"FastChamberReload");
		#### A 0 JM_ReloadGun("PistolMagAmmo", "MO_LowCaliber",18,1);
		#### OPQR 1;
		#### A 0 A_StartSound("pistol/reloadendfast",CHAN_AUTO);
		#### A 0 A_JumpIf(invoker.OwnerHasSpeed(),1);
		#### TUV 1;
		TNT1 A 0;
		Goto ReadyToFire;

	EmptyReload:
		PSR2 A 0;
		#### A 0 A_SetInventory("PistolIsEmpty",1);
		Goto ReloadAnimation;
	ChamberReload:
		#### A 0 A_SetInventory("PistolIsEmpty",0);
		#### A 0 MO_ResetBurstCount();
		#### A 0 JM_ReloadGun("PistolMagAmmo", "MO_LowCaliber",17,1);
		PSR2 PP 1;
		PSR2 Q 3;
		PSR2 RR 1;
		#### A 0 A_StartSound("weapons/pistol/chamberforward",CHAN_AUTO);
		PSR2 S 1;
		PSR2 TTTUU 1;
		#### A 0 A_StartSound("pistol/reloadendfast",CHAN_AUTO);
		#### A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		PSR2 UUVW 1;
		PSR2 XY 1;
		PSR2 Z 1;
		Goto ReadyToFire;

	FastChamberReload:
		#### A 0 A_SetInventory("PistolIsEmpty",0);
		#### A 0 MO_ResetBurstCount();
		#### A 0 JM_ReloadGun("PistolMagAmmo", "MO_LowCaliber",17,1);
		PSR2 P 1;
		PSR2 Q 2;
		#### A 0 A_StartSound("weapons/pistol/chamberforward",CHAN_AUTO);
		PSR2 S 1;
		PSR2 TTTUU 1;
		#### A 0 A_StartSound("pistol/reloadendfast",CHAN_AUTO);
		PSR2 VVW 1;
		PSR2 XY 1;
		PSR2 Z 1;
		Goto ReadyToFire;

	TacticalReload:
		PSTG A 0 A_ZoomFactor(1.0);
		PSTG A 0 {invoker.isZoomed = false;}
		PSTG A 0 A_StartSound("pistol/reloadlift",CHAN_AUTO);
		PSR3 A 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_JumpIf(invoker.OwnerHasSpeed(),1);
		PSR3 BC 1 JM_WeaponReady(WRF_NOFIRE);
		#### A 0 A_JumpIf(invoker.OwnerHasSpeed(),1);
		PSR3 DEF 1 JM_WeaponReady(WRF_NOFIRE);
		PSR3 G 4
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(2);}
		}
		#### A 0 A_JumpIf(invoker.OwnerHasSpeed(),1);
		PSR3 HI 1 JM_WeaponReady(wrf_nofire);
		PSTG A 0 A_StartSound("weapons/pistol/magout",CHAN_AUTO);
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed(),1);
		PSR3 JK 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed(),2);
		PSR3 LMNO 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed(),2);
		PSR3 PQ 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed(),2);
		PSR3 RRR 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG AA 0;
		PSR3 SS 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/pistol/magin",CHAN_AUTO);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 2);
		PSR3 TUUVW 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/pistol/magclick",CHAN_7);
		PSR3 A 0 JM_ReloadGun("PistolMagAmmo", "MO_LowCaliber",18,1);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 2);
		PSR3 XYZ 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("pistol/reloadend",CHAN_AUTO);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 2);
		PSR3 Z 1 JM_WeaponReady(WRF_NOFIRE);
		PSR4 AA 1 JM_WeaponReady(WRF_NOFIRE);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 1);
		PSR4 BCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 2);
		PSR4 EFG 1 JM_WeaponReady(WRF_NOFIRE);
		PSR3 A 0 A_JumpIf(invoker.OwnerHasSpeed(), 1);
		PSR4 HJK 1 JM_WeaponReady(WRF_NOFIRE);
		Goto ReadyToFire;
	Flash:
		PSZG A 0 A_JumpIf(invoker.isZoomed, 2);
		PSTF A 0;
		#### Z 1 BRIGHT A_AttachLightDef('GunLighting', 'GunFireLight');
		TNT1 A 1 BRIGHT A_AttachLightDef('GunLighting', 'GunFireLight');
		Goto GunLightingDone;

 	Spawn:
		PISN A -1;
		Stop;
	FlashKick:
		PSTK A 0 MO_SetSpriteIfEmpty("PS1K");
		#### A 0 
		{
			If(invoker.OwnerHasSpeed()) {return ResolveState("FlashKickFast");}
			return ResolveState(null);
		}
		"####" ABCDE 1;
		"####" F 6;
		"####" EDCBA 1;
		Goto ReadyToFire;
	FlashKickFast:
		"####" A 0;// A_JumpIf(CountInv("PistolMagAmmo") < 1,"FlashKickFastEmpty");
		"####" ABCDE 1;
		"####" FF 1;
		"####" FEDCBA 1;
		Goto ReadyToFire;
	FlashAirKick:
		PSTK A 0 MO_SetSpriteIfEmpty("PS1K");
		---- A 0
		{
			If(invoker.OwnerHasSpeed()) {return ResolveState("FlashAirKickFast");}
			return ResolveState(null);
		}
		#### ABCDEF 1;
		#### F 6;
		#### EDCBA 1;
		Goto ReadyToFire;
	FlashAirKickFast:
		#### ABCDEF 1;
		#### FFFF 1;
		#### FEDCBA 1;
		Goto ReadyToFire;
	FlashEquipmentToss:
		PSTD CBA 1;
		Goto ThrowThatShitForReal;
	}
}

class PistolMagAmmo : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 18;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 18;
		Inventory.Icon "PISNB0";
		+INVENTORY.IGNORESKILL;
	}
}