
class DeagleIsEmpty : MO_Token{}

class MO_Deagle : MO_Weapon// replaces Pistol
{
	const PSP_MUZZLE = -10;

	action void MO_FireDeagle()
	{
		if (!invoker.DepleteAmmo(false, true))
		{return;}
		A_Overlay(PSP_MUZZLE, "MuzzleFlash");
		A_FireBullets (5.6, 0, 1, 70, "UpdatedBulletPuff", FBF_NORANDOM, 0,"MO_BulletTracer",0);
		A_StartSound("weapons/deag/fire", CHAN_WEAPON);
		A_AlertMonsters();
		JM_CheckForQuadDamage();
	}

 	Default
	{
		Weapon.AmmoUse1 1;
		Weapon.AmmoGive2 4;
		Weapon.AmmoType1 "MODeagleAmmo";
		Weapon.AmmoType2 "MO_LowCaliber";
		Obituary "$OBMO_HANDCANNON";
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		+WEAPON.AMMO_OPTIONAL
		Weapon.SelectionOrder 1950;
		Inventory.Pickupmessage "$GOTHANDCANNON";
		Tag "$TAG_HANDCANNON";
		Scale .75;
		Inventory.PickupSound "deag/pickup";
		Inventory.ForbiddenTo "MO_OfficerPlayer";
		VisibleToPlayerClass "MO_SergeantPlayer";
		MO_Weapon.InspectToken "NeverUsedPocketCannon";
	}
	States
	{
	SelectAnimation:
		TNT1 A 0 A_StartSound("weapons/deag/draw",1);
		DGLS ABCD 1;
	Ready:
	ReadyToFire:	
		DGLG A 1 
		{
			if(invoker.isZoomed) {return ResolveState("Ready2");}
			if(PressingFire() && invoker.Ammo1.amount > 0) {return ResolveState("Fire");}
			if(JustPressed(BT_ALTATTACK)) {return ResolveState("AltFire");}
			return JM_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
		Loop;

	Inspect:
		TNT1 A 6;
		DG1I A 1 A_StartSound("weapon/inspectfoley2",0);
		DG1I BCDEFGHHH 1 JM_WeaponReady();
		DG1I IIJJKKLMNN 3 JM_WeaponReady();
		DG1I NNNNNN 1 JM_WeaponReady();
		DG1I O 1 A_StartSound("Deag/FLIP",0);
		DG1I PQRSTUV 1 JM_WeaponReady();
		DG1I VWWXXZZ 3 JM_WeaponReady();
		DG2I AAA 3 JM_WeaponReady();
		DGLG A 0 A_StartSound("weapons/deag/movetoslide",CHAN_AUTO);
		DG2I BCDE 1 JM_WeaponReady();
		DG2I F 1 A_StartSound("weapons/deag/slideback",CHAN_AUTO);
		DG2I GH 1 JM_WeaponReady();
		DG2I IIIIIIIIIIIIII 1 JM_WeaponReady();
		DGLG A 0 A_StartSound("weapons/deag/slideforward",CHAN_AUTO);
		DG2I JKLM 1 JM_WeaponReady();
		DG2I MMMM 1 JM_WeaponReady();
		DGLG A 0 A_StartSound("weapons/deag/reloadend",CHAN_AUTO);
		DG2I OPQRST 1 JM_WeaponReady();
		Goto ReadyToFire;

	Empty: //Made this to prevent an infinite loop crash
		DGZ1 A 0 A_JumpIf(invoker.isZoomed == true, 2);
		DGLG A 0;
		#### AA 0;
		#### A 0 A_JumpIf(invoker.ammo2.amount >= 2, "Reload");
		#### A 0 A_StartSound("weapon/pistolempty",0);
		#### A 1;
		Goto ReadyToFire;

	Deselect:
		TNT1 A 0 A_SetCrosshair(invoker.GetXHair(1));
		DGLS DCBA 1;
		PSTG A 0 A_Lower(12);
		Wait;

	Select:
		TNT1 A 0;
		TNT1 A 0 A_SetCrosshair(invoker.GetXHair(1));
	ContinueSelect:
		TNT1 A 0 MO_Raise();
		TNT1 A 0 JM_CheckInspectIfDone;
		Goto SelectAnimation;

	Fire:
		PSTG A 0 MO_JumpIfLessAmmo(1, "Empty");
		PSTG A 0 A_JumpIf(invoker.isZoomed, "Fire2");
		DGLG B 1 {
			A_WeaponOffset(0,32);
			MO_FireDeagle();
			}
		DGLG C 1 JM_GunRecoil(-1.1, 0);
		DGLG D 1 
		{
			JM_GunRecoil(-1.1, 0);
			MO_EjectCasing("PistolCasing", false, speed: frandom(4,7), offset: (20, 0, -6));
			A_ZoomFactor(0.97);
		}
		DGLG E 1
		{
			JM_GunRecoil(-1.1, 0);
			A_WeaponOffset(0,41);
			A_ZoomFactor(0.99);
		}
		DGLG E 1
		{
			A_WeaponOffset(0,40);
			A_ZoomFactor(1);
		}
		DGLG F 1 A_WeaponOffset(0,39);
		DGLG F 1 A_WeaponOffset(0,38);
		DGLG GH 1{
				A_WeaponOffset(0,37);
				JM_GunRecoil(+.35, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG I 1{
				A_WeaponOffset(0,36);
				JM_GunRecoil(+.25, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG J 1{
				A_WeaponOffset(0,34);
				JM_GunRecoil(+.15, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG KKK 1
		{
				A_WeaponOffset(0,32);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return JM_WeaponReady(WRF_NOPRIMARY);
		}
		PISG A 0 A_JumpIf(PressingFire(), "Fire");// A_ReFire;
		PSTG A 0 MO_JumpIfLessAmmo;
		Goto ReadyToFire;

	Fire2:
		DGZ1 B 1 {
			A_WeaponOffset(0,32);
			MO_FireDeagle();
			}
		DGZ1 C 1 JM_GunRecoil(-.85, 0);
		DGZ1 D 1 Offset(0,28)
		{
			JM_GunRecoil(-.85, 0);
			MO_EjectCasing("PistolCasing", false, speed: frandom(4,7), offset: (15, 0, -2));
			A_ZoomFactor(1.27);
		}
		DGZ1 E 1 Offset(0,24)
		{
			JM_GunRecoil(-.85, 0);
			A_ZoomFactor(1.28);
		}
		DGZ1 E 1 Offset(0,18) A_ZoomFactor(1.3);
		DGZ1 F 1 Offset(0,19);
		DGZ1 F 1;
		DGZ1 G 1 Offset(0,21){
				JM_GunRecoil(+.22, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 H 1 Offset(0,25){
				JM_GunRecoil(+.22, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 I 1{
				A_WeaponOffset(0,27);
				JM_GunRecoil(+.17, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 J 1{
				A_WeaponOffset(0,29);
				JM_GunRecoil(+.12, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 AAA 1
		{
				A_WeaponOffset(0,32);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		PISG A 0
		{
			if(invoker.ADSMode >= 1)
				{
					if(PressingAltFire() && PressingFire())
					{
						invoker.isHoldingAim = true;
						return ResolveState("Fire2");
					}
					if(!PressingAltFire() && invoker.isHoldingAim) {
						return ResolveState("UnZoom");
					}
					else
					{if(PressingFire()) {return ResolveState("Fire2");}}
				}
				else
				{A_ReFire("Fire2");}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
		}
		PSTG A 0 MO_JumpIfLessAmmo;
		Goto ReadyToFire;

		AltFire:
			TNT1 A 0 A_JumpIf(invoker.isZoomed, "UnZoom");
		Zoom:
			TNT1 A 0 {
				invoker.isZoomed = true;
				MO_ZoomBob();
			}
			SMGR A 0 A_StartSound("weapon/adsup",0);
			SMGR A 0 A_ZoomFactor(1.3);
			SMGR A 0 A_SetCrosshair(99);
			DEZA ABC 1;
		Ready2:
			TNT1 A 0 A_JumpIf(invoker.ADSMode <= 0, "ADSToggle");
			TNT1 A 0 A_JumpIf(invoker.ADSMode == 1, "ADSHold");
			DGZ1 AAAAA 1 
			{
				if(JustPressed(BT_ATTACK)) {return ResolveState("Fire");}
				if(PressingFire() && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 A 0 A_JumpIf(invoker.ADSMode == 2 && PressingAltFire() , "ADSHold");

		ADSToggle:
			DGZ1 A 1 
			{
				if(JustPressed(BT_ALTATTACK)) {return ResolveState("UnZoom");}
				if(PressingFire() && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;
		//Hold and Hybrid
		ADSHold:
			TNT1 A 0 {invoker.isHoldingAim = true;}
			DGZ1 A 1 
			{
				if(!PressingAltFire()) {return ResolveState("UnZoom");}
				if(PressingFire() && invoker.Ammo1.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;

		UnZoom:
			TNT1 A 0 
			{
				invoker.isZoomed = False;
				invoker.isHoldingAim = False;
				MO_ResetBob();
			}
			SMGR A 0 A_StartSound("weapon/adsdown",0);
			SMGR A 0 A_ZoomFactor(1.0);
			SMGR A 0 A_SetCrosshair(invoker.GetXHair(1));
			DEZA CBA 1;
			DGLG AAAAA 1
			{
				if(PressingFire()) {return ResolveState("Fire");}
				if(JustPressed(BT_ALTATTACK)) {Return ResolveState("AltFire");}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY);
			Goto ReadyToFire;
	
	MuzzleFlash:
		TNT1 A 0 A_JumpIf(invoker.isZoomed, "MuzzleFlash.Zoomed");
		TNT1 A 0 
		{
			A_OverlayPivot(PSP_MUZZLE, -0.2, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 0.9);
		}
		DGLZ A 1 BRIGHT
		{
			A_AttachLightDef('GunLighting', 'GunFireLight');
			A_OverlayOffset(PSP_MUZZLE, 3,0);
		}
		DGLZ B 1 BRIGHT A_OverlayOffset(PSP_MUZZLE, 1,2);
		Goto GunLightingDone;

	MuzzleFlash.Zoomed:
		TNT1 A 0 
		{
			A_OverlayPivot(PSP_MUZZLE, -0.1, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 1.30);
		}
		DGLZ A 1 BRIGHT 
		{
			A_AttachLightDef('GunLighting', 'GunFireLight');
			A_OverlayOffset(PSP_MUZZLE, -5,2);
		}
		DGLZ B 1 BRIGHT A_OverlayOffset(PSP_MUZZLE, -3,2);
		Goto GunLightingDone;

	Reload:
		TNT1 AAA 0;
		TNT1 A 0 A_WeaponOffset(0,32);
		PSTG A 0 MO_CheckReload(7,2, "EmptyReload", "PartialReload", "ReadyToFire");
	Reload.NormalAnimation:
		PSTG A 0 A_JumpIf(invoker.IsZoomed, "Reload.Zoomed");
		PSTG A 0 A_ZoomFactor(1.0);
		PSTG A 0 A_SetCrosshair(invoker.GetXHair(1));
		DGLR ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_Overlay(-12, "MagazineDrop");
		DGLR F 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_7);
		DGLR GH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		DGLR I 12 
		{
			JM_WeaponReady(WRF_NOFIRE);
			MO_SetHasteTics(6);
		}
		DGLR JKL 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DGLR M 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR N 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR O 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DGLR PQ 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR R 8 
		{
			JM_WeaponReady(WRF_NOFIRE);
			MO_SetHasteTics(4);
		}
		DGLR STUV 1;
		Goto ReadyToFire;

	Reload.Zoomed:
		PSTG A 0 A_ZoomFactor(1.17);
		PSTG A 0 A_WeaponOffset(0,32);
		DEZ2 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ2 E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG AA 0;
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_7);
		DEZ2 FGH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_AUTO);
		DEZ2 H 12 
		{
			JM_WeaponReady(WRF_NOFIRE);
			MO_SetHasteTics(6);
		}
		DEZ2 HG 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEZ2 IJK 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ2 LM 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEZ2 N 8 MO_SetHasteTics(4);
		DEZ2 OPQR 1 A_WeaponOffset(0,32);
		DGZ1 A 1;
		PSTG A 0
		{
			if(invoker.isZoomed && invoker.isHoldingAim && PressingAltFire()) {return ResolveState("ADSHOLD");}
			else if(!PressingAltFire() && invoker.isHoldingAim)
			{
				return ResolveState("UnZoom");
			}
			return ResolveState(null);
		}
		Goto ReadyToFire;

	FastEmptyReload.Zoomed:
		PSTG A 0 A_ZoomFactor(1.17);
		PSTG A 0 A_WeaponOffset(0,32);
		DEZ3 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ3 E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_7);
		DEZ3 FGH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_AUTO);
		DEZ3 H 6;
		DEZ3 HG 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DEZ3 IJK 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ3 LM 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ3 N 1;
		DEZ3 O 4;
		DEZ3 PQ 1 A_WeaponOffset(0,32);
		DEZ3 R 1 A_StartSound("weapons/deag/slideforward",0);
		DEZ3 STTUUU 1;
		DEZ3 VWX 1;
		DEZ3 Y 2;
		PSTG A 0
		{
			if(invoker.isZoomed && invoker.isHoldingAim && PressingAltFire()) {return ResolveState("ADSHOLD");}
			else if(!PressingAltFire() && invoker.isHoldingAim)
			{
				return ResolveState("UnZoom");
			}
			return ResolveState(null);
		}
		Goto ReadyToFire;

	PartialReload: //Tactical Reload
		PSTG A 0 A_StartSound("weapons/deag/reloadraise",CHAN_AUTO);
		PSTG A 0 A_JumpIf(invoker.IsZoomed, "PartialReload.Zoomed");
		DEG1 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 EFG 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 H 1;
		DEG1 I 4;
		DEG1 JKL 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 M 1 
		{
			A_StartSound("weapons/deag/magout",CHAN_AUTO);
			JM_WeaponReady(WRF_NOFIRE);
		}
		DEG1 NO 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 P 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 Q 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 R 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 STU 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 V 4 JM_WeaponReady(WRF_NOFIRE);
		DEG1 W 2 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEG1 X 2 JM_WeaponReady(WRF_NOFIRE);
		DEG1 Y 2;
		DEG1 Z 1;
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEG2 A 1;
		DEG2 B 1;
		DEG2 C 1;
		DEG2 D 3;
		DEG2 EFGH 1;
		PSTG A 0 A_StartSound("weapons/deag/reloadend",CHAN_AUTO);
		DEG2 IJKLM 1;
		Goto ReadyToFire;
	
	PartialReload.Zoomed:
		PSTG A 0 A_ZoomFactor(1.17);
		DEZ1 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 E 8 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 F 1 
		{
			A_StartSound("weapons/deag/magout",CHAN_AUTO);
			JM_WeaponReady(WRF_NOFIRE);
		}
		DEZ1 GH 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 II 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 II 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 J 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 K 5 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 L 2 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEZ1 M 2 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 N 2;
		DEZ1 O 1;
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEZ1 PQR 1;
		DEZ1 S 1;
		DEZ1 T 1;
		DEZ1 U 3;
		DEZ1 VWX 1;
		PSTG A 0 A_ZoomFactor(1.3);
		PSTG A 0 A_StartSound("weapons/deag/reloadend",CHAN_AUTO);
		DEZ1 YZ 1;
		DEZ1 [ 1;
		DEZ1 \ 1;
		DGZ1 A 1;
		PSTG A 0
		{
			if(invoker.isZoomed && invoker.isHoldingAim && PressingAltFire()) {return ResolveState("ADSHOLD");}
			else if(!PressingAltFire() && invoker.isHoldingAim)
			{
				return ResolveState("UnZoom");
			}
			return ResolveState(null);
		}
		Goto Ready2;

//Zoomed Empty Reload normal
	EmptyReload.Zoomed:
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed() && invoker.Ammo1.amount < 1, "FastEmptyReload.Zoomed");
		PSTG A 0 A_ZoomFactor(1.17);
		PSTG A 0 A_StartSound("weapons/DEAG/reloadraise",CHAN_AUTO);
		DEZ4 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ4 E 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ4 F 6;
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_7);
		DEZ4 GHIJJKLM 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ4 N 5 JM_WeaponReady(WRF_NOFIRE);
		DEZ4 N 1;
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEZ4 OPQRS 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",7,2);
		DEZ4 TUV 1;
		DEZ4 W 2;
		DEZ4 X 1 A_StartSound("weapons/deag/movetoslide",CHAN_AUTO);
		DEZ4 YZ 1;
		DEZ4 "[\]" 1;
		DEZ5 ABCDE 1;
		TNT1 A 0 A_StartSound("weapons/deag/slidefull",CHAN_AUTO);
		DEZ5 FGHH 1;
		DEZ5 IJKKLLL 1;
		DEZ5 MN 1;
		PSTG A 0 A_StartSound("weapons/deag/reloadendempty",CHAN_7);
		DEZ5 OP 1;
		DEZ5 Q 1;
		PSTG A 0 A_JumpIf(invoker.isZoomed && invoker.isHoldingAim && PressingAltFire(), "ADSHold");
		Goto Ready2;

	MagazineDrop:
		DGMB A 0 A_JumpIf(invoker.Ammo1.amount < 1, 2);
		DGMA A 0;
		#### ABCD 1;
		Stop;

//True Empty Reload
	EmptyReload:
		PSTG A 0 A_JumpIf(invoker.IsZoomed, "EmptyReload.Zoomed");
		PSTG A 0 A_JumpIf(invoker.OwnerHasSpeed() && invoker.Ammo1.amount < 1, "FastEmptyReload");
		PSTG A 0 A_ZoomFactor(1.0);
		PSTG A 0 A_StartSound("weapons/DEAG/reloadraise",CHAN_AUTO);
		DG1R ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R E 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R FGH 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R I 2;
		DG1R I 2 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_7);
		DEG3 ABCDDEF 1 JM_WeaponReady(WRF_NOFIRE);
		DEG3 G 6 JM_WeaponReady(WRF_NOFIRE);
		DEG3 H 1;
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEG3 IJJKL 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",7,2);
		DEG3 MNO 1 JM_WeaponReady(WRF_NOFIRE);
		DEG3 P 2 JM_WeaponReady(WRF_NOFIRE);
		DEG3 Q 1 A_StartSound("weapons/deag/movetoslide",CHAN_AUTO);
		DEG3 RSTUVWXY 1;
		DEG3 Z 1;
		TNT1 A 0 A_StartSound("weapons/deag/slidefull",CHAN_AUTO);
		DEG4 ABBCC 1;
		DEG4 DEFGHHI 1;
		DEG4 IJ 1;
		PSTG A 0 A_StartSound("weapons/deag/reloadendempty",CHAN_7);
		DEG4 KL 1;
		DGLG A 1;
		PSTG A 0
		{
			if(invoker.isZoomed && !invoker.isHoldingAim) {return ResolveState("Zoom");}
			else if(invoker.isZoomed && invoker.isHoldingAim && PressingAltFire()) {return ResolveState("Zoom");}
			else if(!PressingAltFire())
			{
				invoker.isHoldingAim = false;
				invoker.isZoomed = false;
			}
			return ResolveState(null);
		}
		Goto ReadyToFire;
		
//Fast Reload No Ammo		
	FastEmptyReload:
		DG1R ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_Overlay(-12, "MagazineDrop");
		DG1R F 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_7);
		DG1R GH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		DG1R I 6;
		DG1R J 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG1R KL 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DG1R M 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R N 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R O 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",7,2);
		DG1R PQ 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R R 5;
		DG1R S 1;
		DG1R T 1 A_StartSound("weapons/deag/slideforward",0);
		DG1R UVWXYZ 1;
		DG2R ABC 1;
		DGLG A 1;
		PSTG A 0 A_JumpIf(invoker.isZoomed, "Zoom");
		Goto ReadyToFire;

 	Spawn:
		DGLP Z -1;
		Stop;
	FlashKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashKickFast");
		DGLG A 1;
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FFF 1 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashKickFast:
		DGLG A 1;
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FF 1 JM_WeaponReady();
		DGLK EDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashAirKickFast");
		DGLG A 1;
		DGLK ABCDEF 1 JM_WeaponReady();
		DGLK FFF 2 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKickFast:
		DGLG A 1;
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FF 2 JM_WeaponReady();
		DGLK EDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;

	FlashEquipmentToss:
		DGLS DCBA 1;
		Goto ThrowThatShitForReal;
	}
}

class MODeagleAmmo : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 8;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 8;
		Inventory.Icon "DGLPZ0";
		+INVENTORY.IGNORESKILL;
	}
}