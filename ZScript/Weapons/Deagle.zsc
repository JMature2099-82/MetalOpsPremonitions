
class DeagleIsEmpty : MO_Token{}

class MO_Deagle : JMWeapon// replaces Pistol
{
	const PSP_MUZZLE = -10;

	action void MO_FireDeagle()
	{
		if (!invoker.DepleteAmmo(true, true))
		{
			if(invoker.Ammo2.amount < 1 && invoker.Ammo1.amount < 1)
			{
					A_StartSound("weapon/pistolempty",0);
					SetWeaponState("NoAmmo");
					return;
			}
			SetWeaponState("Reload");
			return;
		}
		A_Overlay(PSP_MUZZLE, "MuzzleFlash");
		A_FireBullets (5.6, 0, 1, 70, "UpdatedBulletPuff", FBF_NORANDOM, 0,"MO_BulletTracer",0);
		A_StartSound("weapons/deag/fire", CHAN_WEAPON);
		A_AlertMonsters();
		JM_CheckForQuadDamage();
	}

 	Default
	{
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive 4;
		Weapon.AmmoType1 "MO_LowCaliber";
		Weapon.AmmoType2 "MODeagleAmmo";
		Obituary "$OBMO_HANDCANNON";
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		Weapon.SelectionOrder 1950;
		Inventory.Pickupmessage "$GOTHANDCANNON";
		Tag "$TAG_HANDCANNON";
		Scale .75;
		Inventory.PickupSound "weapons/deag/draw";
		Inventory.ForbiddenTo "MO_OfficerPlayer";
		VisibleToPlayerClass "MO_SergeantPlayer";
	}
	States
	{
	ContinueSelect:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise();
		Goto Ready;
	Ready:	
	SelectAnimation:
		TNT1 A 0 A_StartSound("weapons/deag/draw",1);
		DGLS ABCD 1;
	ReadyToFire:	
		DGLG A 1 
		{
			if(invoker.isZoomed) {return ResolveState("Ready2");}
			if(PressingFire() && invoker.Ammo2.amount > 0) {return ResolveState("Fire");}
			if(JustPressed(BT_ALTATTACK)) {return ResolveState("AltFire");}
			return JM_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
		Loop;

	NoAmmo: //Made this to prevent an infinite loop crash
		DGZ1 A 0;
		DGLG A 1
		{
			if(invoker.isZoomed) {JM_SetWeaponSprite("DGZ1");}
		}
		Goto ReadyToFire;

	Deselect:
		TNT1 A 0 A_SetCrosshair(invoker.GetXHair(1));
		DGLS DCBA 1;
		PSTG A 0 A_Lower(12);
		Wait;
	Select:
		TNT1 A 0;
		TNT1 A 0 A_SetCrosshair(invoker.GetXHair(1));
		Goto ClearAudioAndResetOverlays;
	Fire:
		PSTG A 0 A_JumpIf(invoker.isZoomed, "Fire2");
		DGLG B 1 {
			A_WeaponOffset(0,32);
			MO_FireDeagle();
			}
		DGLG C 1 JM_GunRecoil(-1.1, 0);
		DGLG D 1 
		{
			JM_GunRecoil(-1.1, 0);
			MO_EjectCasing("PistolCasing", false, speed: frandom(4,7), offset: (20, 0, -6));
			A_ZoomFactor(0.97);
		}
		DGLG E 1
		{
			JM_GunRecoil(-1.1, 0);
			A_WeaponOffset(0,41);
			A_ZoomFactor(0.99);
		}
		DGLG E 1
		{
			A_WeaponOffset(0,40);
			A_ZoomFactor(1);
		}
		DGLG F 1 A_WeaponOffset(0,39);
		DGLG F 1 A_WeaponOffset(0,38);
		DGLG GH 1{
				A_WeaponOffset(0,37);
				JM_GunRecoil(+.35, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG I 1{
				A_WeaponOffset(0,36);
				JM_GunRecoil(+.25, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG J 1{
				A_WeaponOffset(0,34);
				JM_GunRecoil(+.15, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGLG KKK 1
		{
				A_WeaponOffset(0,32);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return JM_WeaponReady(WRF_NOPRIMARY);
		}
		PISG A 0 A_JumpIf(PressingFire(), "Fire");// A_ReFire;
		PSTG A 0 MO_CheckMag;
		Goto ReadyToFire;

	Fire2:
		DGZ1 B 1 {
			A_WeaponOffset(0,32);
			MO_FireDeagle();
			}
		DGZ1 C 1 JM_GunRecoil(-.85, 0);
		DGZ1 D 1 Offset(0,28)
		{
			JM_GunRecoil(-.85, 0);
			MO_EjectCasing("PistolCasing", false, speed: frandom(4,7), offset: (15, 0, -2));
			A_ZoomFactor(1.17);
		}
		DGZ1 E 1 Offset(0,24)
		{
			JM_GunRecoil(-.85, 0);
			A_ZoomFactor(1.185);
		}
		DGZ1 E 1 Offset(0,18) A_ZoomFactor(1.2);
		DGZ1 F 1 Offset(0,19);
		DGZ1 F 1;
		DGZ1 G 1 Offset(0,21){
				JM_GunRecoil(+.22, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 H 1 Offset(0,25){
				JM_GunRecoil(+.22, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 I 1{
				A_WeaponOffset(0,27);
				JM_GunRecoil(+.17, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 J 1{
				A_WeaponOffset(0,29);
				JM_GunRecoil(+.12, 0);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		DGZ1 AAA 1
		{
				A_WeaponOffset(0,32);
				If(JustPressed(BT_ATTACK))
				{return ResolveState("Fire");}
				Return ResolveState(null);
		}
		PISG A 0
		{
			if(invoker.ADSMode >= 1)
				{
					if(PressingAltFire() && PressingFire())
					{
						invoker.isHoldingAim = true;
						return ResolveState("Fire2");
					}
					if(!PressingAltFire() && invoker.isHoldingAim) {
						return ResolveState("UnZoom");
					}
					else
					{if(PressingFire()) {return ResolveState("Fire2");}}
				}
				else
				{A_ReFire("Fire2");}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
		}
		PSTG A 0 MO_CheckMag;
		Goto ReadyToFire;

		AltFire:
			TNT1 A 0 A_JumpIf(invoker.isZoomed, "UnZoom");
		Zoom:
			TNT1 A 0 {
				invoker.isZoomed = true;
				MO_ZoomBob();
			}
			SMGR A 0 A_StartSound("weapon/adsup",0);
			SMGR A 0 A_ZoomFactor(1.2);
			SMGR A 0 A_SetCrosshair(99);
			DEZA ABC 1;
		Ready2:
			TNT1 A 0 A_JumpIf(invoker.ADSMode <= 0, "ADSToggle");
			TNT1 A 0 A_JumpIf(invoker.ADSMode == 1, "ADSHold");
			TNT1 A 0 A_JumpIf(PressingFire(), "Fire");
			DGZ1 AAAAA 1 
			{
				if(JustPressed(BT_ATTACK)) {return ResolveState("Fire");}
				if(PressingFire() && invoker.Ammo2.amount > 0) {
				return ResolveState("Fire");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 A 0 A_JumpIf(invoker.ADSMode == 2 && PressingAltFire() , "ADSHold");

		ADSToggle:
			DGZ1 A 1 
			{
				if(JustPressed(BT_ALTATTACK)) {return ResolveState("UnZoom");}
				if(PressingFire() && invoker.Ammo2.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;
		//Hold and Hybrid
		ADSHold:
			TNT1 A 0 {invoker.isHoldingAim = true;}
			DGZ1 A 1 
			{
				if(!PressingAltFire()) {return ResolveState("UnZoom");}
				if(PressingFire() && invoker.Ammo2.amount > 0) {
				return ResolveState("Fire2");
				}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY);
			}
			Loop;

		UnZoom:
			TNT1 A 0 
			{
				invoker.isZoomed = False;
				invoker.isHoldingAim = False;
				MO_ResetBob();
			}
			SMGR A 0 A_StartSound("weapon/adsdown",0);
			SMGR A 0 A_ZoomFactor(1.0);
			SMGR A 0 A_SetCrosshair(invoker.GetXHair(1));
			DEZA CBA 1;
			DGLG AAAAA 1
			{
				if(PressingFire()) {return ResolveState("Fire");}
				if(JustPressed(BT_ALTATTACK)) {Return ResolveState("AltFire");}
				return JM_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			}
			TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY);
			Goto ReadyToFire;
	
	MuzzleFlash:
		TNT1 A 0 A_JumpIf(invoker.isZoomed, "MuzzleFlash.Zoomed");
		DGLZ A 1 BRIGHT
		{
			A_AttachLightDef('GunLighting', 'GunFireLight');
			A_OverlayPivot(PSP_MUZZLE, -0.14, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 0.9);
			A_OverlayOffset(PSP_MUZZLE, 0,2);
		}
		DGLZ B 1 BRIGHT
		{
			A_OverlayPivot(PSP_MUZZLE, -0.14, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 0.9);
			A_OverlayOffset(PSP_MUZZLE, 1,4);
		}
		Goto GunLightingDone;

	MuzzleFlash.Zoomed:
		DGLZ A 1 BRIGHT 
		{
			A_AttachLightDef('GunLighting', 'GunFireLight');
			A_OverlayPivot(PSP_MUZZLE, -0.14, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 1.30);
			A_OverlayOffset(PSP_MUZZLE, -2,12);
		}
		DGLZ B 1 BRIGHT
		{
			A_OverlayPivot(PSP_MUZZLE, -0.14, 0.1);
			A_OverlayPivotAlign(PSP_MUZZLE, PSPA_CENTER, PSPA_CENTER);
			A_OverlayScale(PSP_MUZZLE, 1.30);
			A_OverlayOffset(PSP_MUZZLE, -2,12);
		}
		Goto GunLightingDone;

	Reload:
		PSTG A 0 A_JumpIfInventory("MODeagleAmmo",invoker.Ammo2.MaxAmount-1,"ReadyToFire");
		PSTG A 0 A_JumpIfInventory("MO_LowCaliber",1,1);
		Goto ReadyToFire;
		TNT1 A 0
		{
			if(!invoker.OwnerHasSpeed() && invoker.Ammo2.amount > 1)
			{
				return ResolveState("PartialReload");
			}
			else if(invoker.OwnerHasSpeed() && invoker.Ammo2.amount < 1)
			{
				return ResolveState("FastEmptyReload");
			}
			else if(invoker.Ammo2.amount < 1)
			{
				return ResolveState("EmptyReload");
			}
			return ResolveState(null);
		}
	Reload.NormalAnimation:
		PSTG A 0 A_JumpIf(invoker.IsZoomed, "Reload.Zoomed");
		PSTG A 0 A_ZoomFactor(1.0);
		PSTG A 0 A_SetCrosshair(invoker.GetXHair(1));
		DGLR ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_Overlay(-12, "MagazineDrop");
		DGLR FGH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		DGLR I 14 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(8);}
		}
		DGLR JKL 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DGLR M 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR N 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR O 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DGLR PQ 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR R 8 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(4);}
		}
		DGLR STUV 1;
		Goto ReadyToFire;

	Reload.Zoomed:
		PSTG A 0 A_ZoomFactor(1.15);
		PSTG A 0 A_WeaponOffset(0,32);
		DEZ2 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ2 E 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ2 FGH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		DEZ2 H 14 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(8);}
		}
		DEZ2 HG 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEZ2 IJK 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DEZ2 LM 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEZ2 M 8 
		{
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(4);}
		}
		DEZ2 NOPQR 1 A_WeaponOffset(0,32);
		Goto ReadyToFire;

	PartialReload: //Tactical Reload
		PSTG A 0 A_StartSound("weapons/deag/reloadraise",CHAN_AUTO);
		PSTG A 0 A_JumpIf(invoker.IsZoomed, "PartialReload.Zoomed");
		DEG1 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 EFG 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 H 1;
		DEG1 I 4;
		DEG1 JKL 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 M 1 
		{
			A_StartSound("weapons/deag/magout",CHAN_AUTO);
			JM_WeaponReady(WRF_NOFIRE);
		}
		DEG1 NO 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 P 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 Q 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 R 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 STU 1 JM_WeaponReady(WRF_NOFIRE);
		DEG1 V 4 JM_WeaponReady(WRF_NOFIRE);
		DEG1 W 2 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEG1 X 2 JM_WeaponReady(WRF_NOFIRE);
		DEG1 Y 2;
		DEG1 Z 1;
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEG2 A 1;
		DEG2 B 1;
		DEG2 C 1;
		DEG2 D 3;
		DEG2 EFGH 1;
		PSTG A 0 A_StartSound("weapons/deag/reloadend",CHAN_AUTO);
		DEG2 IJKLM 1;
		Goto ReadyToFire;
	
	PartialReload.Zoomed:
		PSTG A 0 A_ZoomFactor(1.15);
		DEZ1 ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 E 8 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 F 1 
		{
			A_StartSound("weapons/deag/magout",CHAN_AUTO);
			JM_WeaponReady(WRF_NOFIRE);
		}
		DEZ1 GH 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 II 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 II 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 J 1 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 K 5 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 L 2 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEZ1 M 2 JM_WeaponReady(WRF_NOFIRE);
		DEZ1 N 2;
		DEZ1 O 1;
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",8,2);
		DEZ1 PQR 1;
		DEZ1 S 1;
		DEZ1 T 1;
		DEZ1 U 3;
		DEZ1 VWX 1;
		PSTG A 0 A_ZoomFactor(1.2);
		PSTG A 0 A_StartSound("weapons/deag/reloadend",CHAN_AUTO);
		DEZ1 YZ 1;
		DEZ1 [ 1;
		DEZ1 \ 1;
		Goto Ready2;

	MagazineDrop:
		DGMB A 0 A_JumpIf(invoker.Ammo2.amount < 1, 2);
		DGMA A 0;
		#### ABCD 1;
		Stop;

//True Empty Reload
	EmptyReload:
		PSTG A 0 A_ZoomFactor(1.0);
		PSTG A 0 A_StartSound("weapons/DEAG/reloadraise",CHAN_AUTO);
		DG1R ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R E 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R FGH 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R I 4;
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		PSTG A 0 A_StartSound("weapons/deag/magoutempty",CHAN_7);
		DEG3 ABCDDEF 1 JM_WeaponReady(WRF_NOFIRE);
		DEG3 G 6 JM_WeaponReady(WRF_NOFIRE);
		DEG3 H 1;
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DEG3 IJJKL 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",7,2);
		DEG3 MNO 1 JM_WeaponReady(WRF_NOFIRE);
		DEG3 P 2 JM_WeaponReady(WRF_NOFIRE);
		DEG3 Q 1 A_StartSound("weapons/deag/movetoslide",CHAN_AUTO);
		DEG3 RSTUVWXY 1;
		DEG3 Z 1 A_StartSound("weapons/deag/slideback",CHAN_AUTO);
		DEG4 ABBC 1;
		TNT1 A 0 A_StartSound("weapons/deag/slideforward",CHAN_AUTO);
		DEG4 CDEFGHHI 1;
		PSTG A 0 A_StartSound("weapons/deag/reloadendempty",CHAN_7);
		DEG4 IJKL 1;
		DGLG A 1;
		PSTG A 0 A_JumpIf(invoker.isZoomed, "Zoom");
		Goto ReadyToFire;
		
//Fast Reload No Ammo		
	FastEmptyReload:
		DG1R ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R E 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_Overlay(-12, "MagazineDrop");
		DG1R FGH 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		DG1R I 8;
		DG1R J 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG1R KL 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		DG1R M 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R N 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG1R O 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "MO_LowCaliber",7,2);
		DG1R PQ 1 JM_WeaponReady(WRF_NOFIRE);
		DG1R R 5;
		DG1R S 1;
		DG1R T 1 A_StartSound("weapons/deag/slideforward",0);
		DG1R UVWXXYZ 1;
		DG2R ABC 1;
		DGLG A 1;
		PSTG A 0 A_JumpIf(invoker.isZoomed, "Zoom");
		Goto ReadyToFire;

 	Spawn:
		DGLP Z -1;
		Stop;
	FlashKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashKickFast");
		DGLG A 1;
		DGLK ABCD 1 JM_WeaponReady();
		DGLK FFFF 1 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashKickFast:
		DGLG A 1;
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FF 1 JM_WeaponReady();
		DGLK EDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashAirKickFast");
		DGLG A 1;
		DGLK ABCDEF 1 JM_WeaponReady();
		DGLK FFF 2 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKickFast:
		DGLG A 1;
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FF 1 JM_WeaponReady();
		DGLK EDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	}
}

class MODeagleAmmo : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 8;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 8;
		Inventory.Icon "DGLPZ0";
		+INVENTORY.IGNORESKILL;
	}
}